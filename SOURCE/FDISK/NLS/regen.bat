@ECHO OFF

:: THIS SCRIPT REGENERATES THE TRANSLATIONS FILE (FDISK.LNG) AS WELL AS THE
:: DEFLANG.C FILE THAT IS NEEDED AT FDISK'S COMPILE TIME.

:: LET'S MAKE SURE THAT UTF8TOCP IS AVAILABLE
UTF8TOCP DUPA DUPA > nul
IF NOT ERRORLEVEL 1 GOTO ERR_MISSINGUTF

:: *** CONVERT UTF-8 TXT FILES TO PROPER CODEPAGES ***************************

UTF8TOCP 850 DE_UTF8.TXT > DE.TXT
UTF8TOCP 437 EN_UTF8.TXT > EN.TXT
UTF8TOCP maz PL_UTF8.TXT > PL.TXT

:: ***************************************************************************

:: "en" must come first here! first item is the reference language.
..\SVARLANG\TLUMACZ en de pl

:: CLEAN UP CONVERTED FILES
DEL ??.TXT

:: MOVE DEFLANG.C AND FDISK.LNG TO FDISK'S MAIN DIRECTORY
TYPE DEFLANG.C > ..\DEFLANG.C
DEL DEFLANG.C
MOVE /Y OUT.LNG ..\FDISK.LNG

GOTO DONE

:ERR_MISSINGUTF
ECHO.
ECHO ERROR: UTF8TOCP NOT FOUND IN PATH. PLEASE INSTALL UTF8TOCP AND TRY AGAIN.
ECHO        http://utf8tocp.sourceforge.net

:DONE
